apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'preferencecheckbox'

    publishedGroupId = 'in.shaapps.librariez'
    libraryName = 'custom-checked-textview'
    artifact = 'custom-checked-textview'

    libraryDescription = 'An alternative for android default checked textview'

    siteUrl = 'https://github.com/akbarsha03/Custom_CheckedTextView'
    gitUrl = 'https://github.com/akbarsha03/Custom_CheckedTextView.git'

    libraryVersion = '1.0.0-beta'

    developerId = 'akbarsha03'
    developerName = 'Akbar Sha Ebrahim'
    developerEmail = 'akbar.sha03@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.libraryVariants.all { variant ->
        task("generate${variant.name}Javadoc", type: Javadoc) {
            description "Generates Javadoc for $variant.name."
            source = variant.javaCompile.source
            ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
            classpath = files(variant.javaCompile.classpath.files) + files(ext.androidJar)
            options.links("http://docs.oracle.com/javase/7/docs/api/");
            options.links("http://d.android.com/reference/");
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            failOnError false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.0'
}

apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
